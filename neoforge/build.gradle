plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

base {
    archivesName = "utility-belt-neoforge"
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven {
        name = "NeoForge"
        url = "https://maven.neoforged.net"
    }
}

dependencies {
    neoForge libs.neoforge
    modImplementation libs.architectury.neoforge
    modImplementation libs.jamlib.neoforge
    modImplementation libs.accessories.neoforge

    forgeRuntimeLibrary "blue.endless:jankson:1.2.3" // Dependency of JamLib
    forgeRuntimeLibrary "blue.endless:jankson:1.2.2" // Dependency of Accessories
    forgeRuntimeLibrary "io.wispforest:endec:0.1.9" // Dependency of Accessories
    forgeRuntimeLibrary "io.wispforest.endec:gson:0.1.6" // Dependency of Accessories
    forgeRuntimeLibrary "io.wispforest.endec:jankson:0.1.6" // Dependency of Accessories
    forgeRuntimeLibrary "io.wispforest.endec:netty:0.1.5" // Dependency of Accessories

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
}

if (System.getenv("CURSEFORGE_API_KEY") != null && System.getenv("MODRINTH_API_KEY") != null) {
    unifiedPublishing {
        project {
            displayName = "V${project.version} (NeoForge ${project.minecraft_version})"
            changelog = rootProject.file("CHANGELOG.md").text
            releaseType = project.version.contains("beta") ? "beta" : "release"
            gameVersions = [project.minecraft_version, *(project.additional_minecraft_versions == "" ? [] : project.additional_minecraft_versions.split(","))]
            gameLoaders = ["neoforge"]

            mainPublication tasks.remapJar

            relations {
                depends {
                    curseforge = "architectury-api"
                    modrinth = "architectury-api"
                }

                depends {
                    curseforge = "jamlib"
                    modrinth = "jamlib"
                }

                depends {
                    curseforge = "accessories"
                    modrinth = "accessories"
                }
            }

            curseforge {
                token = System.getenv("CURSEFORGE_API_KEY")
                id = "664053"
            }

            modrinth {
                token = System.getenv("MODRINTH_API_KEY")
                id = "9ewe2JHJ"
            }
        }
    }
}

processResources {
    def substitutions = [
            "version"             : project.version,
            "minecraft_version"   : project.minimum_minecraft_version,
            "neoforge_version"    : project.minimum_minecraft_version.substring(2),
            "architectury_version": project.minimum_architectury_api_version,
            "jamlib_version"      : project.minimum_jamlib_version,
            "accessories_version" : project.minimum_accessories_version_neoforge
    ]

    filesMatching("META-INF/neoforge.mods.toml") {
        expand substitutions
    }

    substitutions.each { inputs.property it.key, it.value }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
