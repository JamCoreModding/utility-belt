plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    forge()
}

base {
    archivesName = "utility-belt-forge"
}

loom {
    forge {
        convertAccessWideners = true
        mixinConfig "utility_belt.mixins.json"
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge libs.forge
    modApi libs.architectury.forge
    modImplementation libs.jamlib.forge
    modImplementation libs.baubly.forge
    include libs.baubly.forge
    compileOnly libs.mixin.extras.common
    annotationProcessor libs.mixin.extras.common
    implementation libs.mixin.extras.forge
    include libs.mixin.extras.forge

    // Dependency of JamLib
    forgeRuntimeLibrary "blue.endless:jankson:1.2.3"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

if (System.getenv("CURSEFORGE_API_KEY") != null && System.getenv("MODRINTH_API_KEY") != null) {
    unifiedPublishing {
        project {
            displayName = "V${project.version} (Forge ${project.minecraft_version})"
            changelog = rootProject.file("CHANGELOG.md").text
            releaseType = project.version.contains("beta") ? "beta" : "release"
            gameVersions = [project.minecraft_version]
            gameLoaders = ["forge"]

            mainPublication tasks.remapJar

            relations {
                depends {
                    curseforge = "architectury-api"
                    modrinth = "architectury-api"
                }

                depends {
                    curseforge = "jamlib"
                    modrinth = "jamlib"
                }

                depends {
                    curseforge = "curios"
                    modrinth = "curios"
                }
            }

            curseforge {
                token = System.getenv("CURSEFORGE_API_KEY")
                id = "664053"
            }

            modrinth {
                token = System.getenv("MODRINTH_API_KEY")
                id = "9ewe2JHJ"
            }
        }
    }
}

processResources {
    filesMatching("mods.toml") {
        expand "minecraft_version": project.minecraft_version,
            "architectury_version": libs.versions.architectury.get(),
            "jamlib_version": libs.versions.jamlib.get()
    }

    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "architectury_version", libs.versions.architectury.get()
    inputs.property "jamlib_version", libs.versions.jamlib.get()
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
