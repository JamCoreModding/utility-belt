plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    forge()
}

base {
    archivesName = "utility-belt-forge"
}

loom {
    forge {
        mixinConfig "utility_belt.mixins.json"
        mixinConfig "utility_belt.client.mixins.json"
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        name = "Forge"
        url = "https://maven.minecraftforge.net"
    }
}

dependencies {
    forge libs.forge
    modImplementation libs.architectury.forge
    modImplementation libs.jamlib.forge
    modImplementation(libs.accessories.forge) { transitive = false }

    forgeRuntimeLibrary("io.wispforest:endec:0.1.8")
    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.5")
    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.4")
//    forgeRuntimeLibrary("io.wispforest:endec:0.1.5.1")
//    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.3.1")
//    forgeRuntimeLibrary("io.wispforest.endec:jankson:0.1.3.1")
//    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.2")

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.3"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.5.3"))
    modRuntimeOnly("dev.su5ed.sinytra.fabric-api:fabric-api:0.92.2+1.11.8+1.20.1")

    forgeRuntimeLibrary "blue.endless:jankson:1.2.3" // Dependency of JamLib

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

if (System.getenv("CURSEFORGE_API_KEY") != null && System.getenv("MODRINTH_API_KEY") != null) {
    unifiedPublishing {
        project {
            displayName = "V${project.version} (Forge ${project.minecraft_version})"
            changelog = rootProject.file("CHANGELOG.md").text
            releaseType = project.version.contains("beta") ? "beta" : "release"
            gameVersions = [project.minecraft_version, *(project.additional_minecraft_versions == "" ? [] : project.additional_minecraft_versions.split(","))]
            gameLoaders = ["forge"]

            mainPublication tasks.remapJar

            relations {
                depends {
                    curseforge = "architectury-api"
                    modrinth = "architectury-api"
                }

                depends {
                    curseforge = "jamlib"
                    modrinth = "jamlib"
                }

                depends {
                    curseforge = "accessories"
                    modrinth = "accessories"
                }
            }

            curseforge {
                token = System.getenv("CURSEFORGE_API_KEY")
                id = "664053"
            }

            modrinth {
                token = System.getenv("MODRINTH_API_KEY")
                id = "9ewe2JHJ"
            }
        }
    }
}

processResources {
    def substitutions = [
            "version"             : project.version,
            "minecraft_version"   : project.minimum_minecraft_version,
            "architectury_version": project.minimum_architectury_api_version,
            "jamlib_version"      : project.minimum_jamlib_version,
            "accessories_version" : project.minimum_accessories_version_forge
    ]

    filesMatching("META-INF/mods.toml") {
        expand substitutions
    }

    substitutions.each { inputs.property it.key, it.value }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
